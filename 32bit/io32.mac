extern   proc_GetCh
extern   proc_PutInt, proc_GetInt
extern   proc_PutLInt, proc_GetLInt

;;;-------------------------------------------------------------------------
; Constants

STDIN           equ 0
STDOUT          equ 1
STDERR          equ 2
 
SYS_READ        equ 3
SYS_WRITE       equ 4
SYS_EXIT        equ 1

;;;-------------------------------------------------------------------------
; .STARTUP
; Seup global access code

%macro .STARTUP 0

        global  _start
	_start:

%endmacro

;;;-------------------------------------------------------------------------
; .DATA
; Initialized data

%macro .DATA 0

        SECTION .data

%endmacro

;;;-------------------------------------------------------------------------
; .UDATA
; Uninitialized data

%macro .UDATA 0

        SECTION .bss

%endmacro

;;;-------------------------------------------------------------------------
; .CODE
; Code section

%macro .CODE 0

        SECTION .data
        SECTION .bss
        SECTION .text

%endmacro

;;;-------------------------------------------------------------------------
; .EXIT
; terminate program and return 0

%macro .EXIT 0

        mov     eax, SYS_EXIT
        xor     ebx, ebx  ;set ebx to zero as return code
        int     0x80

%endmacro

;;;-------------------------------------------------------------------------
; nwln
; print newline character

%macro nwln 0 

	;'''''''''''''''''''''''''''''''
	SECTION .data
	%%NEWLINE db 10

	;'''''''''''''''''''''''''''''''
	SECTION .text
	pusha
	mov     eax, SYS_WRITE
	mov     ebx, STDOUT
	mov     ecx, %%NEWLINE
	mov     edx, 1  ;length of newline character
	int     0x80
	popa

%endmacro

;;;-------------------------------------------------------------------------
; PutCh source
; source: value / register / memory
; size: 8bits
; Displays the character located at source

%macro PutCh 1

	;'''''''''''''''''''''''''''''''
	SECTION .bss
	%%CHAR resb 1

	;'''''''''''''''''''''''''''''''
	SECTION .text
	pusha
	mov     al, %1  ;pointer to haracter
	mov     [%%CHAR], al
	mov     eax, SYS_WRITE
	mov     ebx, STDOUT
	mov     ecx, %%CHAR
	mov     edx, 1  ;length of a character
	int     0x80
	popa

%endmacro

;;;-------------------------------------------------------------------------
; PutStr source
; source: memory
; size: variable
; Displays the NULL-terminated string at source

%macro PutStr 1

        pusha
        pushf
        mov     ecx, %1   ;pointer to string
        mov     ebx, STDOUT
        mov     edx, 1    ;length of a character

        %%rpt_putstr:
        mov     eax, SYS_WRITE
        cmp     byte [ecx], 0  ;null-termination check
        je  %%skip_putstr
        int     0x80      ;print a character
        inc     ecx       ;next character
        jmp %%rpt_putstr

        %%skip_putstr:
        popf
        popa

%endmacro

;;;-------------------------------------------------------------------------
; GetStr dest [,buf_size] 
; dest: memory
; size: variable
; Reads a carriage-return-terminated string into dest and stores it
; as a NULL-terminated string. Maximum string length is buf_size-1

%macro  GetStr  1-2 81

	;'''''''''''''''''''''''''''''''
	SECTION .bss
	%%TMP resb 256

	;'''''''''''''''''''''''''''''''
	SECTION .text
	pusha
	pushf
	mov     eax, SYS_READ
	mov     ebx, STDIN
	mov     ecx, %%TMP
	mov     edx, 256   ;length of temp buffer
	int     0x80       ;input string to temp buffer

	mov     ecx, %2    ;length of buffer parameter
	dec     ecx
	mov     ebx, %1    ;buffer parameter
	mov     esi, %%TMP

	%%loop_getstr:
	mov     al, byte [esi]
	cmp     al, 10     ;search for newline
	je  %%done_getstr
	mov     byte [ebx], al
	inc     ebx
	inc     esi
	loop    %%loop_getstr

	%%done_getstr:
	mov     byte [ebx], 0  ;replace newline with NULL
	popf
	popa

%endmacro

;;;-------------------------------------------------------------------------
; GetCh dest
; dest: register / memory
; size: 8 bits
; Reads a character into dest

%macro GetCh 1

	;'''''''''''''''''''''''''''''''
	SECTION .bss
	%%CHAR resb 1

	;'''''''''''''''''''''''''''''''
	SECTION .text

	pushf
	pusha

	mov    eax, SYS_READ
	mov    ebx, STDIN
	mov    ecx, %%CHAR
	mov    edx, 0x100 ;len 256 char
	int    0x80

	popa
%ifidni %1,al
	mov     al,[%%CHAR]
%elifidni %1,ah
	mov     ah,[%%CHAR]
%else
	push    ax
	mov     al,[%%CHAR]
	mov	%1, al
	pop     ax
%endif
	pushf

%endmacro

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;-------------------------------------------------------------------
%macro  PutInt  1
        push    AX
        mov     AX,%1
        call    proc_PutInt
        pop     AX
%endmacro
;;-------------------------------------------------------------------


;;-------------------------------------------------------------------
%macro  GetInt  1
%ifnidni %1,AX
        push    AX
        call    proc_GetInt
        mov     %1,AX
	  pop     AX
%else 
        call  proc_GetInt
%endif
%endmacro
;;-------------------------------------------------------------------

;;-------------------------------------------------------------------
%macro  PutLInt  1
        push    EAX
        mov     EAX,%1
        call    proc_PutLInt
        pop     EAX
%endmacro
;;-------------------------------------------------------------------

;;-------------------------------------------------------------------
%macro  GetLInt  1
%ifnidni %1,EAX
        push    EAX
        call    proc_GetLInt
        mov     %1,EAX
	  pop     EAX
%else 
        call  proc_GetLInt
%endif
%endmacro
;;-------------------------------------------------------------------

